apply plugin: 'org.jetbrains.intellij'

ext.enableMavenPublishing = false

intellij {
    pluginName = 'RESTflow'
    version = '223.8617.56' //'2022.3.2'
    updateSinceUntilBuild = false
    type = 'IC'

    sandboxDir = 'idea-sandbox'

    plugins = ['java', 'Groovy']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    compileOnly project(':core')
}

idea.module {
    excludeDirs += file('idea-sandbox')
}

configurations {
    restflowJars
}
dependencies {
    restflowJars project(path: ':core', configuration: 'default')
    restflowJars project(path: ':frontend', configuration: 'default')
}

task collectRestflowJars(type: Sync) {
    dependsOn project(':core').build, project(':core').sourceJar

    destinationDir file("$buildDir/shipped-jars")
    def basePath = 'net/netconomy/tools/restflow/shipped-jars'

    def index = []
    index.add System.currentTimeMillis().intdiv(1000) as String

    into(basePath) {
        from configurations.restflowJars
        from project(':core').tasks.sourceJar
        from project(':frontend').tasks.sourceJar

        eachFile {FileCopyDetails fcd ->
            index.add fcd.name
        }
    }
    preserve {
        include "$basePath/index"
    }

    doLast {
        file("$destinationDir/$basePath/index").setText(index.join('\n'))
    }
}

sourceSets.main.resources.srcDir file("$tasks.collectRestflowJars.destinationDir")
tasks.processResources.dependsOn tasks.collectRestflowJars

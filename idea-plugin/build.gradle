apply plugin: 'org.jetbrains.intellij'

ext.enableMavenPublishing = false

intellij {
    pluginName = 'RESTflow'
    version = '233.11799.300' // 2023.3.1
    updateSinceUntilBuild = false
    type = 'IC'

    sandboxDir = 'idea-sandbox'

    plugins = ['java', 'Groovy']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    compileOnly project(':core')
}

idea.module {
    excludeDirs += file('idea-sandbox')
}

configurations {
    restflowJars
}
dependencies {
    restflowJars project(path: ':core', configuration: 'default')
    restflowJars project(path: ':frontend', configuration: 'default')
}

def shippedJarsBuildDir = file("$buildDir/shipped-jars")
def shippedJarsPrefix = 'net/netconomy/tools/restflow/jars'
task consoleJar(type: Jar) {
    def classesDir = sourceSets.main.java.classesDirectory.get().asFile
    dependsOn tasks.compileJava
    destinationDir = file("${sourceSets.main.output.resourcesDir}/$shippedJarsPrefix")
    archiveFileName = "console.jar"
    from(classesDir) {
        include "net/netconomy/tools/restflow/integrations/idea/console/external/**.class"
    }
}
tasks.processResources.dependsOn tasks.consoleJar
task collectRestflowJars(type: Sync) {
    dependsOn project(':core').build, project(':core').sourceJar

    destinationDir shippedJarsBuildDir

    def index = []
    index.add UUID.randomUUID()

    into("$shippedJarsPrefix/lib/classes") {
        from configurations.restflowJars
        eachFile {FileCopyDetails fcd ->
            index.add "classes/$fcd.name"
        }
    }
    into("$shippedJarsPrefix/lib/sources") {
        from project(':core').tasks.sourceJar
        from project(':frontend').tasks.sourceJar
        eachFile {FileCopyDetails fcd ->
            index.add "sources/$fcd.name"
        }
    }
    preserve {
        include "$shippedJarsPrefix/lib/index.txt"
    }

    doLast {
        file("$destinationDir/$shippedJarsPrefix/lib/index.txt").setText(index.join('\n') + '\n')
    }
}

sourceSets.main.resources.srcDir file("$tasks.collectRestflowJars.destinationDir")
tasks.processResources.dependsOn tasks.collectRestflowJars
